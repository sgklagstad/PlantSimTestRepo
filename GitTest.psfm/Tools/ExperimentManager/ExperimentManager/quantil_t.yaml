InternalClassType: Method
Coordinate3D: [31.450000000000003,-30.85,0]
Name: quantil_t
Origin: 42f08459-a1ee-41e0-98b3-d5158dee9b83
Program: |+1
 -- Exact quantil of the Student-t-tDistribution for confidence intervals
 -- ExpMgr 15.2.8 and Statistical Tools 15.2.6 
 -- Regula falsi, Interval Bisection Method / Intervallhalbierungsverfahren
 param p, df:real -> real
 --like p := 0.975; df := 4 results 2.77644510521539 after 57 steps
 var eps:real := 1e-14
 var numSteps:integer
 
 var low, upp, mid, f_low, f_upp, f_mid:real -- f_ = error function of the probability
 low := 0.0
 upp := 1000 -- betai = 0.999999999997 ~ (1 - 3e-12) 
 -- clearConsole 
 var h:real := sqrt(low * low + df)
 f_low :=  p - betai(df/2, df/2, (low + h)/(2 * h))
 
 h := sqrt(upp * upp + df)
 f_upp :=  p - betai(df/2, df/2, (upp + h)/(2 * h))
 
 repeat
 	numSteps += 1
 	mid := (low + upp)/2
 	h := sqrt(mid * mid + df)
 	f_mid := p - betai(df/2, df/2, (mid + h)/(2 * h))
 	
 	if f_mid * f_low < 0
 		upp := mid
 		f_upp := f_mid
 	else
 		low := mid
 		f_low := f_mid
 	end
 	--print "Check after ",numSteps," steps: ",f_low * f_upp < 0,"  ",upp - low
 	
 until  upp - low < eps OR f_low * f_upp >= 0 OR numSteps > 100
 
 -- print mid," after ",numSteps," steps"
 return mid
RandomSeed: 290
UUID: f4b8d544-4e42-4ba9-9598-1b6eea359ce6
$SequenceNumber: 9
3D:
 Mirror: [false,false,false]
